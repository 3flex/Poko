import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

group 'dev.drewhamilton.extracare'
version '0.1.0'

buildscript {
    repositories {
        google()
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'dev.drewhamilton.extracare:extracare-plugin-gradle'
        classpath "com.android.tools.build:gradle:3.6.1"
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        jcenter()
    }

    pluginManager.withPlugin("org.jetbrains.kotlin.jvm") {
        project.tasks.withType(KotlinCompile).configureEach {
            kotlinOptions {
                jvmTarget = '1.8'
                freeCompilerArgs += ['-progressive']
            }
        }
    }
}

subprojects {
    configurations.all {
        resolutionStrategy {
            dependencySubstitution {
                // The Kotlin plugin will try to add the extracare-compiler-plugin to dependencies with a version
                // We have it local to this project, so we want it to just substitute that with the local project
                substitute module('dev.drewhamilton.extracare:extracare-compiler-plugin') with project(':extracare-compiler-plugin')
                substitute module('dev.drewhamilton.extracare:data-api-annotations') with project(':data-api-annotations')
            }
        }
    }
}
